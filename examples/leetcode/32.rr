# DESIGN ADD AND SEARCH WORDS DATA STRUCTURE
# 
# Design a data structure that supports adding new words
# and finding if a string matches any previously added string.

inc lib.Trie;

Find(self, string);

Dot(self, string)
{
    keys := Keys(self);
    foreach(key : keys)
    {
        if(key == "end") # NOT A VALID KEY.
        {
            continue;
        }
        if(Find(self[key], string))
        {
            ret true;
        }
    }
    ret false;
}

Find(self, string)
{
    node := &self;
    index := 0;
    foreach(c : string)
    {
        if(c == ".")
        {
            next := string[index + 1 : Len(string)];
            ret Dot(*node, next);
        }
        if(!Exists(*node, c))
        {
            ret false;
        }
        node = &(*node)[c];
        index += 1;
    }
    ret node@end == true;
}

Main()
{
    trie := {};
    Trie_Insert(trie, "word");
    Trie_Insert(trie, "woaa");
    Trie_Insert(trie, "wobb");
    Assert(Find(trie, "wor") == false);
    Assert(Find(trie, "wo.d"));
    Assert(Find(trie, "wo.a"));
    Assert(Find(trie, "wo.b"));
    Assert(Find(trie, "...d"));
    Assert(Find(trie, "...a"));
    Assert(Find(trie, "...b"));
    Assert(Find(trie, "...."));
    Assert(Find(trie, ".....") == false);
    Assert(Find(trie, ".") == false);
    Assert(Find(trie, "") == false);
    ret 0;
}
