# REVERSE A LINKED LIST
#
# Given the head of a singly linked list,
# reverse the list, and return the reversed list.

inc lib.List;

Reverse(nodes)
{
    rev := null;
    node := nodes;
    while(node != null)
    {
        List_Push(rev, List_Node(node@value));
        node = node@next;
    }
    ret rev;
}

Populate(with)
{
    nodes := null;
    for(i := Len(with) - 1; i >= 0; i -= 1)
    {
        List_Push(nodes, List_Node(with[i]));
    }
    ret nodes;
}

Test(a, b)
{
    ret List_Equal(Reverse(Populate(a)), Populate(b));
}

Main()
{
    Assert(Test([0, 1, 2, 3   ], [3, 2, 1, 0    ]) == true);
    Assert(Test([0, 1, 2, 3   ], [3, 2, 1, 0, -1]) == false);
    Assert(Test([0, 1, 2, 3, 4], [3, 2, 1, 0    ]) == false);
    Assert(Test([], []) == true);
    Assert(Test([1], [1]) == true);
    Assert(Test([1, 2], [2, 1]) == true);
    ret 0;
}
