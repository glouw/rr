Alpha := {
    "a" : 1, "b" : 1, "c" : 1, "d" : 1, "e" : 1, "f" : 1, "g" : 1, "h" : 1, "i" : 1, "j" : 1, "k" : 1, "l" : 1, "m" : 1,
    "n" : 1, "o" : 1, "p" : 1, "q" : 1, "r" : 1, "s" : 1, "t" : 1, "u" : 1, "v" : 1, "w" : 1, "x" : 1, "y" : 1, "z" : 1, 
    "A" : 1, "B" : 1, "C" : 1, "D" : 1, "E" : 1, "F" : 1, "G" : 1, "H" : 1, "I" : 1, "J" : 1, "K" : 1, "L" : 1, "M" : 1,
    "N" : 1, "O" : 1, "P" : 1, "Q" : 1, "R" : 1, "S" : 1, "T" : 1, "U" : 1, "V" : 1, "W" : 1, "X" : 1, "Y" : 1, "Z" : 1, 
};

Space := {
    " "  : 1,
    "\n" : 1,
    "\r" : 1,
};

Punct := {
    "!" : 1, "\"": 1, "#" : 1, "$" : 1, "%" : 1, "&" : 1,
    "'" : 1, "(" : 1, ")" : 1, "*" : 1, "+" : 1, "," : 1,
    "-" : 1, "." : 1, "/" : 1, ":" : 1, "," : 1, ";" : 1,
    "?" : 1, "@" : 1, "[" : 1, "\\": 1, "]" : 1, "^" : 1,
    "_" : 1, "`" : 1, "{" : 1, "|" : 1, "}" : 1, "~" : 1,
};

Lookup(char, dict) {
    string := copy(char);
    if(dict[string] == null) {
        ret false;
    }
    else {
        ret true;
    }
}

IsAlpha(char) { ret Lookup(char, Alpha); }
IsSpace(char) { ret Lookup(char, Space); }
IsPunct(char) { ret Lookup(char, Punct); }

Split(string) {
    word := "";
    words := [];
    size := len(string);
    for(i := 0; i < size; i += 1) {
        char := string[i];
        if(IsAlpha(char)) {
            word += char;
        }
        elif(IsSpace(char) || IsPunct(char)) {
            if(len(word) > 0) {
                words += word;
            }
            word = "";
        }
    }
    ret words;
}

WordCount(words) {
    count := {};
    size := len(words);
    for(i := 0; i < size; i += 1) {
        word := words[i];
        if(count[word] == null) {
            count[word] := 1;
        }
        else {
            count[word] += 1;
        }
    }
    ret count;
}

main() {
    name := "waroftheworlds.txt";
    file := open(name, "r");
    if(good(file)) {
        size := len(file);
        raw := read(file, size);
        words := Split(raw);
        count := WordCount(words);
        print("{}\nwords: {} " % [count, len(words)]);
        ret 0;
    }
    else {
        print("could not open file {}" % [name]);
        ret 1;
    }
}
