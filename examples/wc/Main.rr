ALPHA := {
    "a", "b", "c", "d", "e", "f", "g", "h", "i", "j", "k", "l", "m",
    "n", "o", "p", "q", "r", "s", "t", "u", "v", "w", "x", "y", "z", 
    "A", "B", "C", "D", "E", "F", "G", "H", "I", "J", "K", "L", "M",
    "N", "O", "P", "Q", "R", "S", "T", "U", "V", "W", "X", "Y", "Z", 
};

SPACE := { " ", "\n", "\r" };

PUNCT := {
    "!", "\"", "#", "$", "%", "&", "'", "(", ")", "*", "+", ",", "-",
    ".", "/", ":", ",", ";", "?", "@", "[", "\\", "]", "^", "_", "`",
    "{", "|", "}", "~",
};

Lookup(char, dict) {
    string := Copy(char); # Chars promote to strings with a copy.
    if(dict[string] == null) {
        ret false;
    }
    else {
        ret true;
    }
}

IsAlpha(char) { ret Lookup(char, ALPHA); }
IsSpace(char) { ret Lookup(char, SPACE); }
IsPunct(char) { ret Lookup(char, PUNCT); }

Split(string) {
    word := "";
    words := [];
    size := Len(string);
    for(i := 0; i < size; i += 1) {
        char := string[i];
        if(IsAlpha(char)) {
            word += char;
        }
        elif(IsSpace(char) || IsPunct(char)) {
            if(Len(word) > 0) {
                words += Copy(word);
            }
            word = "";
        }
    }
    ret words;
}

WordCount(words) {
    count := {};
    size := Len(words);
    for(i := 0; i < size; i += 1) {
        word := words[i];
        if(count[word] == null) {
            count[word] := 1;
        }
        else {
            count[word] += 1;
        }
    }
    ret count;
}

Main() {
    name := "waroftheworlds.txt";
    file := Open(name, "r");
    if(Good(file)) {
        size := Len(file);
        raw := Read(file, size);
        words := Split(raw);
        count := WordCount(words);
        Print("{}\nwords: {} " % [count, Len(words)]);
        ret 0;
    }
    else {
        Print("could not open file {}" % [name]);
        ret 1;
    }
}
